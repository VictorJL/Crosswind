class button{
    attr actors
    attr widget

    attr mouse
    attr trigger

    attr on_trigger

    def button(name, origin, size, text, on_trigger){
        this.widget := stage.create_group(name)
        this.actors := Map()

        this.widget.set_origin(origin)
        this.widget.set_size(size)

        eval_file("scripts/actors/environment.chai")
        eval_file("scripts/actors/input.chai")

        this.actors["button"] := control(origin, size, "button", "button.json")

        var text_size := vec3(size.x - size.x * 0.20f, size.y - size.y * 0.20f, 0.0f)
        this.actors["label"] := label("label", origin, text_size, text)
        this.actors["label"].get().get_text("text").set_size(text_size)

        this.mouse := mouse()
        this.trigger = false
        this.on_trigger := on_trigger


        this.widget.construct(fun(){}, fun(){}, fun[this](delta){ this.logic(delta) })

        for_each(this.actors, fun[this](i) { this.widget.add_actor(i.first, i.second.get()) })

    }

    def set_text(new_text){
        this.actors["label"].set_text(new_text)
    }

    def logic(delta){

        if(this.mouse.is_down(this.widget)){

            if(!this.trigger){
                this.actors["button"].get().get_model("button").change_animation("button_on")
                this.trigger = true
            }

        } else if (this.mouse.is_over(this.widget) && this.trigger){
            this.actors["button"].get().get_model("button").change_animation("button_off")
            this.trigger = false
            on_trigger(this)()
        } else {
            this.trigger = false
            this.actors["button"].get().get_model("button").change_animation("button_off")
        }

    }

    def get(){
        return this.widget
    }

}
