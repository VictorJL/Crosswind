return fun(name, text, origin, size, on_trigger){
    var button := stage.create_actor()

    button.set_origin(origin)
    button.set_size(size)

    button.add_model("button", origin, size, "green_button.json")

    var button_color := vec3(1.0f, 1.0f, 1.0f)

    button.add_text(name,
                    text,
                    origin,
                    button_color,
                    24,
                    core.filesystem.get_file_path("ui_font.ttf"))

    var mouse_down = false

    button.construct(fun(){}, fun(){}, fun[button, core, mouse_down, on_trigger](delta){

        var current_camera := stage.get_scene("current").get_camera("current")
        var world_coordinates := current_camera.convert_screen_to_world(core.input.get_mouse_coordinates())

        var mouse_x = world_coordinates.x
        var mouse_y = world_coordinates.y

        var x = button.get_origin().x
        var y = button.get_origin().y

        var w = button.get_size().x
        var h = button.get_size().y

        if(mouse_x >= x - w / 2.0f && mouse_x <= x + w / 2.0f && mouse_y >= y - h / 2.0f && mouse_y <= y + h / 2.0f){

            if(core.input.is_key_down("mouse_left")){
                if(!mouse_down){
                    button.get_model("button").change_animation("button_on")
                    mouse_down = true
                }

            } else {
                if(mouse_down){
                    button.get_model("button").change_animation("button_off")
                    mouse_down = false
                    on_trigger()
                }
            }
        } else {
            mouse_down = false
            button.get_model("button").change_animation("button_off")
        }
    });

    return button
}